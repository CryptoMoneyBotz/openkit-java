/**
 * Copyright 2018-2021 Dynatrace LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.name

            from components.java

            artifact sourceJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            pom {
                name = "${project.group}:${project.name}"
                packaging = 'jar'
                description = 'Dynatrace OpenKit - Java Reference Implementation'
                url = 'https://github.com/Dynatrace/openkit-java'

                scm {
                    url = 'https://github.com/Dynatrace/openkit-java.git'
                }

                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        id = 'Dynatrace'
                        name = 'Dynatrace LLC'
                        email = 'openkit@dynatrace.com'
                    }
                }
            }
        }
    }
}

artifacts {
    archives sourceJar, javadocZip, javadocJar
}

if (project.hasProperty("ossrhUsername") && project.ossrhUsername && project.hasProperty("ossrhPassword") && project.ossrhPassword) {
    signing {
        sign publishing.publications.mavenJava
    }
    nexusPublishing {
        packageGroup = "com.dynatrace"
        useStaging = true
        repositories {
            sonatype {
                nexusUrl = uri("https://oss.sonatype.org/service/local/")
                snapshotRepositoryUrl = uri("https://oss.sonatype.org/content/repositories/snapshots/")
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}
